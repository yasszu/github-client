apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

def packageName = "ysuzuki.githubclient"

def localProperties() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties
}

def localHost = localProperties().getProperty("local_host")

android {

    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    flavorDimensions "default"

    defaultConfig {
        applicationId packageName
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "HOST", "\"https://api.github.com/\""
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "HOST", "\"https://api.github.com/\""
        }
    }

    productFlavors {
        product {
            dimension "default"
            applicationId packageName
            buildConfigField "String", "HOST", "\"${product_host}\""
        }
        develop {
            dimension "default"
            applicationId packageName + ".dev"
            buildConfigField "String", "HOST", "\"" + localHost + "\""
        }
    }

    dataBinding {
        enabled = true
    }

    kapt {
        mapDiagnosticLocations = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Android support libraries.
    implementation 'androidx.core:core-ktx:1.0.1'

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // Data binding
    kapt 'androidx.databinding:databinding-compiler:3.4.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Square
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.moshi:moshi:1.4.0'

    // Retrofit2
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation "com.squareup.retrofit2:converter-moshi:2.2.0"
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'

    // Dagger2
    implementation "com.google.dagger:dagger:2.9"
    kapt "com.google.dagger:dagger-compiler:2.9"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // Test libraries
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation "org.robolectric:robolectric:3.3.2"

    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.annotation:annotation:1.0.2'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
repositories {
    mavenCentral()
    google()
}
